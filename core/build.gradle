apply plugin: 'java-library'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = appJavaCompatibility

dependencies {
    api project(':SPD-classes')
    implementation project(':services')

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testImplementation project(':desktop')
}

def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)

test {
    useJUnitPlatform()

    maxHeapSize = '1G'
    maxParallelForks = 8
    failFast = true

    systemProperty 'Specification-Title', appName
    systemProperty 'Implementation-Title', appPackageName
    systemProperty 'Specification-Version', appVersionName + "-INDEV"
    systemProperty 'Implementation-Version', appVersionCode

    if (osName.contains('mac')) {
        jvmArgs '-XstartOnFirstThread'
    }
}

sourceSets {
    test {
        java.srcDirs = ['src/test/java', 'src/main/java']
    }
}
